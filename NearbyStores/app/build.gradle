apply plugin: 'com.android.application'
apply plugin: 'com.huawei.agconnect'

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"
    defaultConfig {
        applicationId "com.huawei.hms.nearby.beacondemo"
        minSdkVersion 26
        targetSdkVersion 31
        versionCode 1021
        versionName "1.0.2.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file('xxx.xxx')
            storePassword 'xxxxxx'
            keyAlias 'xxxxxx'
            keyPassword 'xxxxxx'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "mode"
    productFlavors {
        product {
            dimension "mode"
            //China nearby server
            buildConfigField("String", "MESSAGE_HOST", '"nearby-drcn.cloud.huawei.com.cn"')
            buildConfigField("String", "MESSAGE_PORT", '""')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        def buildType = variant.buildType.name
        def flavorName = getFlavorName()
        variant.outputs.all { output ->
            outputFileName = "BeaconManager_" + flavorName + "_" + buildType + "_" + defaultConfig.versionName + ".apk"
        }
    }

    packagingOptions {
        // 去重文件：META-INF/proguard/coroutines.pro
        exclude 'META-INF/com.android.tools/proguard/coroutines.pro'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.squareup.retrofit2:retrofit:2.7.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.0'
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.mikesamuel:json-sanitizer:1.2.0'
    implementation 'com.huawei.hms:awareness:1.0.4.301'
    implementation 'com.huawei.hms:base:6.9.0.300'
    implementation 'com.huawei.hms:nearby:6.10.0.300'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    api 'io.jsonwebtoken:jjwt-api:0.11.1'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.1'
    runtimeOnly('io.jsonwebtoken:jjwt-orgjson:0.11.1') {
        exclude group: 'org.json', module: 'json'
    }
}
